use ProtossZealot as knowledge.
use "../GenericMods/GenericPercepts" as module.

module ProtossZealotEvent {
	
	%%%%%%%%%%%%%%% --------------------------------------- Percepts --------------------------------------- %%%%%%%%%%%%%%%
	
	% The generic percepts most units use
	if true then GenericPercepts.
	
	% Insert enemy when one is perceived
	forall percept(unit(false, Type,Id,Health,Shields,Conditions)), bel(not(enemy(Type,Id,Health,Shields))) 
	do insert(enemy(Type,Id,Health,Shields)).
	forall bel(enemy(Type, Id, Health, Shields)) , not(percept(unit(false, Type,Id,Health,Shields, Conditions)))
	do delete(enemy(Type,Id,Health, Shields)).	
	
	% The handling of the position 
	if percept(status(_,_,_,_,X1,Y1)), bel(position(X2,Y2), (X1 \= X2 ; Y1 \= Y2)) then delete(position(X2,Y2)) + insert(position(X1,Y1)).
	
	% Perceives enemy base (NOTE: ONLY WORKS ON MAP WITH 2 BASES)
	if bel(not(enemyBase(_, _)), nexusPosition(X, Y), base(X1, Y1, true, _), 
	distance(X, Y, X1, Y1, Res), Res > 20) then insert(enemyBase(X1, Y1)).

	
	%%%%%%%%%%%%%%% --------------------------------------- Messaging --------------------------------------- %%%%%%%%%%%%%%%


		% If don't know nexus location yet, start asking Nexus
	if not(bel(nexusPosition(_, _))) then (protossNexus).send?(nexusPosition).
	
	% If nexus location is told, insert it into the knowledge base
	if (_).sent:(nexusPosition(X, Y)) then insert(nexusPosition(X, Y)).

	if (Name).sent(ready(Id)), not(bel(ready(Id))) then insert(ready(Id)).	
	
	if bel(position(X1,Y1), groupLocation(X,Y), distance(X, Y, X1, Y1, D), D < 3000, self(Id,_))
		then print("I am ready") + insert(ready(Id)) + (protossZealot).send(ready).
	
	%reply if ready
	forall (Name).sent?(ready), bel(self(Id,_), ready(Id)) do (Name).send:(ready(Id)).
	
	if bel(allReady) then print("All aboard!") + insert(attack).
	
	%%%%%%%%%%%%%%% ----------------------------------------- GOALS ---------------------------------------- %%%%%%%%%%%%%%%
	
	% If not at the groupLocation yet, insert it to knowledge base and adopt goal to go there
	if bel(not(groupLocation(X, Y)), chokepoint(X, Y), enemyBase(X1, Y1), distance(X, Y, X1, Y1, D), D < 30, D > 25) 
	then insert(groupLocation(X, Y)) + adopt(attack(X, Y)).
	
	% If bel to attack then adopt goal to attack enemy base
	if not(goal(attack(X, Y))), bel(attack, enemyBase(X, Y)) then adopt(attack(X, Y)).
}